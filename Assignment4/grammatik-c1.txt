******************************************************************************
*   Grammatik für die Sprache C-1 (Link weiter unten) ohne Linksrekursion.   *
*   Token innerhalb "" oder <> Klammern sind Terminalsymbole, alles andere   *
*   Nichtterminalsymbole. Das € Zeichen steht für Epsilon.                   *
*                                                                            *
*   https://amor.cms.hu-berlin.de/~kunert/lehre/material/c1-grammar.php     *
******************************************************************************


program               ::= declassignment ";" program
                      |   functiondefinition program
                      |   €
functiondefinition    ::= type id "(" parameterlist ")" "{" statementlist "}"
                      |   type id "(" ")" "{" statementlist "}"
parameterlist         ::= type id parameterlist_s
parameterlist_s       ::= "," type id parameterlist_s
                      |   €
functioncall 	        ::= id "(" ")"
                      |   id "(" assignment assignment_s ")"
assignment_s          ::= "," assignment assignment_s
                      |   €
statementlist 	      ::= block statementlist
                      |   €
block                 ::= "{" statementlist "}"
                      |   statement
statement 	          ::= ifstatement
                      |   forstatement
                      |   whilestatement
                      | 	returnstatement ";"
                      |   dowhilestatement ";"
                    	| 	printf ";"
                      |   declassignment ";"
                    	| 	statassignment ";"
                      |   functioncall ";"
statblock             ::= "{" statementlist "}"
                      |   statement
ifstatement 	        ::= <KW_IF> "(" assignment ")" statblock
                      |   <KW_IF> "(" assignment ")" statblock <KW_ELSE> statblock
forstatement          ::= <KW_FOR> "(" statassignment ";" expr ";" statassignment ")" statblock
                      |   <KW_FOR> "(" declassignment ";" expr ";" statassignment ")" statblock
dowhilestatement      ::= <KW_DO> statblock <KW_WHILE> "(" assignment ")"
whilestatement        ::= <KW_WHILE> "(" assignment ")" statblock
returnstatement 	    ::= <KW_RETURN> assignment
                      |   <KW_RETURN>
printf 	              ::= <KW_PRINTF> "(" assignment ")"
                      |   <KW_PRINTF> "(" <CONST_STRING> ")"
declassignment        ::= type id
                      |   type id "=" assignment
type 	                ::= <KW_BOOLEAN>
	                    | 	<KW_FLOAT>
	                    | 	<KW_INT>
	                    | 	<KW_VOID>
statassignment 	      ::= id "=" assignment
assignment 	          ::= id "=" assignment
                      |   expr
expr 	                ::= simpexpr
                      |   simpexpr "==" simpexpr
                      |   simpexpr "!=" simpexpr
                      |   simpexpr "<=" simpexpr
                      |   simpexpr ">=" simpexpr
                      |   simpexpr "<" simpexpr
                      |   simpexpr ">" simpexpr
simpexpr 	            ::= "-" term term_s
                      |   term term_s
term_s                ::= "+" term term_s
                      |   "-" term term_s
                      |   "||" term term_s
                      |   €
term 	                ::= factor factor_s
factor_s              ::= "*" factor factor_s
                      |   "/" factor factor_s
                      |   "&&" factor factor_s
                      |   €
factor 	              ::= <CONST_INT>
                      | 	<CONST_FLOAT>
                    	| 	<CONST_BOOLEAN>
                    	| 	functioncall
                    	| 	id
                    	| 	"(" assignment ")"
id                    ::= <ID>

________________________________________________________________________________

Nichtterminalsymbol       First
_ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _
program                   <KW_BOOLEAN>,<KW_FLOAT>,<KW_INT>,<KW_VOID>,<EOF>
A                         €,<KW_BOOLEAN>,<KW_FLOAT>,<KW_INT>,<KW_VOID>
functiondefinition        <KW_BOOLEAN>,<KW_FLOAT>,<KW_INT>,<KW_VOID>
functioncall              <ID>
statementlist             €,"{",<KW_IF>,<KW_RETURN>,<KW_PRINTF>,<ID>
B                         €,"{",<KW_IF>,<KW_RETURN>,<KW_PRINTF>,<ID>
block                     "{",<KW_IF>,<KW_RETURN>,<KW_PRINTF>,<ID>
statement                 <KW_IF>,<KW_RETURN>,<KW_PRINTF>,<ID>
K                         "=","("
ifstatement               <KW_IF>
returnstatement           <KW_RETURN>
C                         €,<ID>,"-",<CONST_INT>,<CONST_FLOAT>,<CONST_BOOLEAN>,<ID>,"("
printf                    <KW_PRINTF>
type                      <KW_BOOLEAN>,<KW_FLOAT>,<KW_INT>,<KW_VOID>
statassignment            <ID>
assignment                <ID>,"-",<CONST_INT>,<CONST_FLOAT>,<CONST_BOOLEAN>,<ID>,"("
expr                      "-",<CONST_INT>,<CONST_FLOAT>,<CONST_BOOLEAN>,<ID>,"("
D                         €,"==","!=","<=",">=","<",">"
E                         "==","!=","<=",">=","<",">"
simpexpr                  "-",<CONST_INT>,<CONST_FLOAT>,<CONST_BOOLEAN>,<ID>,"("
F                         €,"-"
G                         "||","-","+"
H                         €,"||","-","+"
term                      <CONST_INT>,<CONST_FLOAT>,<CONST_BOOLEAN>,<ID>,"("
I                         "*","/","&&"
J                         €,"*","/","&&"
factor                    <CONST_INT>,<CONST_FLOAT>,<CONST_BOOLEAN>,<ID>,"("



VORSICHT, die FOLLOW Menge ist nicht korrekt

Nichtterminalsymbol       Follow
_ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _
program                   $,
A                         <EOF>
functiondefinition        <KW_BOOLEAN>,<KW_FLOAT>,<KW_INT>,<KW_VOID>
functioncall              ";","*","/","&&"
statementlist             "}"
B                         "}"
block                     "{",<KW_IF>,<KW_RETURN>,<KW_PRINTF>,<ID>,"}"
statement                 "{",<KW_IF>,<KW_RETURN>,<KW_PRINTF>,<ID>
K                         ";"
ifstatement               "}"
returnstatement           ";"
C                         ";"
printf                    ";"
type                      <ID>
statassignment            ";"
assignment                ")",";"
expr                      ")",";"
D                         ")",";"
E                         "-",<CONST_INT>,<CONST_FLOAT>,<CONST_BOOLEAN>,<ID>,"("
simpexpr                  "==","!=","<=",">=","<",">",")",";"
F                         <CONST_INT>,<CONST_FLOAT>,<CONST_BOOLEAN>,<ID>,"("
G                         <CONST_INT>,<CONST_FLOAT>,<CONST_BOOLEAN>,<ID>,"("
H                         "||","-","+","==","!=","<=",">=","<",">",")",";"
term                      "||","-","+","==","!=","<=",">=","<",">",")",";"
I                         <CONST_INT>,<CONST_FLOAT>,<CONST_BOOLEAN>,<ID>,"("
J                         "||","-","+","==","!=","<=",">=","<",">",")",";"
factor                    "*","/","&&"
